apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion versions.compileSdk
  defaultConfig {
    applicationId "com.yuyakaido.android.gaia"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode versions.versionCode
    versionName versions.versionName

    def properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    buildConfigField "String", "GITHUB_API_ENDPOINT", "\"https://api.github.com/\""
    buildConfigField "String", "GITHUB_GRAPHQL_ENDPOINT", "\"https://api.github.com/graphql\""
    buildConfigField "String", "GITHUB_AUTH_ENDPOINT", "\"https://github.com/login/oauth/\""
    buildConfigField "String", "GITHUB_CLIENT_ID", "\"${properties.getProperty('GITHUB_CLIENT_ID')}\""
    buildConfigField "String", "GITHUB_CLIENT_SECRET", "\"${properties.getProperty('GITHUB_CLIENT_SECRET')}\""
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  packagingOptions {
    exclude 'META-INF/*.kotlin_module'
  }
  dataBinding {
    enabled = true
  }
}

dependencies {
  // Module
  implementation project(':core-android')
  implementation project(':modules:auth:service')
  implementation project(':modules:gateway:service')
  implementation project(':modules:home:service')
  implementation project(':modules:repo:service')
  implementation project(':modules:repo-detail:service')
  implementation project(':modules:profile:service')

  // Kotlin
  implementation libs.kotlin.stdlib

  // AndroidX
  implementation libs.androidx.appcompat
  implementation libs.androidx.lifecycle.extensions
  kapt libs.androidx.lifecycle.compiler

  // Networking
  implementation libs.okhttp.core
  implementation libs.okhttp.logger
  implementation libs.retrofit.core
  implementation libs.retrofit.rxjava
  implementation libs.retrofit.gson

  // Reactive Programming
  implementation libs.rx.java
  implementation libs.rx.kotlin
  implementation libs.rx.android

  // Dependency Injection
  implementation libs.dagger.core.runtime
  kapt libs.dagger.core.compiler
  implementation libs.dagger.android.runtime
  implementation libs.dagger.android.support
  kapt libs.dagger.android.compiler

  // View
  implementation libs.groupie
}
