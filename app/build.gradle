apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'

android {
  buildToolsVersion versions.android.buildTools
  compileSdkVersion versions.android.compileSdk
  defaultConfig {
    targetSdkVersion versions.android.targetSdk
    minSdkVersion versions.android.minSdk
    applicationId versions.app.applicationId
    versionCode versions.app.versionCode
    versionName versions.app.versionName
    // https://dagger.dev/hilt/compiler-options#disable-install-in-check
    // https://qiita.com/Horie1024/items/bb10677eac5769497122
    javaCompileOptions {
      annotationProcessorOptions {
        arguments += [
          "dagger.hilt.disableModulesHaveInstallInCheck": "true"
        ]
      }
    }
  }
  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
    release {
      storeFile file('debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }
  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
    release {
      signingConfig signingConfigs.release
    }
  }
  productFlavors {
    flavorDimensions 'default'
    gaia {}
    ouranos {}
  }
  androidExtensions {
    experimental = true
  }
  buildFeatures {
    dataBinding true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }
}

dependencies {
  // Modules - Core
  implementation project(':cores:core')
  implementation project(':cores:domain')
  implementation project(':cores:infrastructure')
  implementation project(':cores:presentation')

  // Modules - Feature
  implementation project(':features:auth')
  implementation project(':features:article')
  implementation project(':features:comment')
  implementation project(':features:community')
  implementation project(':features:user')
  implementation project(':features:search')
  implementation project(':features:vote')
  implementation project(':features:support')

  // AndroidX
  implementation libs.androidx.room.core
  implementation libs.androidx.room.ktx
  kapt libs.androidx.room.compiler
  implementation libs.androidx.hilt.viewmodel
  kapt libs.androidx.hilt.compiler

  // Dependency Injection
  implementation libs.dagger.java.core
  kapt libs.dagger.java.compiler
  implementation libs.dagger.android.core
  implementation libs.dagger.android.support
  kapt libs.dagger.android.compiler
  implementation libs.dagger.hilt.core
  kapt libs.dagger.hilt.compiler
}